* Setting *	* Value *
library	ApplicationLauncher	${app}	WITH NAME	myAppLauncher
library	ApplicationLauncher	${app}	1sec	WITH NAME	oneSec
library	ApplicationLauncher	${app}	60sec	WITH NAME	tenSec
library	ApplicationLauncher	${app}	WITH NAME	reimport
library	ApplicationLauncher	${app}	10sec	WITH NAME	passingArguments
library	ApplicationLauncher	${app}	WITH NAME	multipleLauncher

*Variable*	*Value*
${app}	org.robotframework.jvmconnector.mocks.MyApplication
${remoteLib}	org.robotframework.jvmconnector.keyword.JvmconnectorKeywords
${timeoutMsg}=	Could not connect to application ${app}

* Test Case *	* Action *	* Argument *
Starts Application And Imports Remote Library
	myAppLauncher.startApplication
	myAppLauncher.importRemoteLibrary	${remoteLib}	WITH NAME	myApp
	remoteLibraryShouldBeImported	myApp
	[teardown]	runKeywordAndIgnoreError	myApp.stopJvm

Importing Remote Library Times Outs If Application Is Not Started
	shouldFailInApproximatelyOneSecond	${timeoutMsg}	oneSec.importRemoteLibrary	${remoteLib}	WITH NAME	thisShouldFail

Importing Remote Library Times Outs If Application Is Stopped
	tenSec.startApplication
	tenSec.importRemoteLibrary	${remoteLib}	WITH NAME	stoppedApp
	runKeywordAndIgnoreError	stoppedApp.stopJvm
	shouldFailInApproximatelyTenSeconds	${timeoutMsg}	tenSec.importRemoteLibrary	${remoteLib}	WITH NAME	thisShouldFail

Reimports After Restart
	reimport.startApplication
	reimport.importRemoteLibrary	${remoteLib}	WITH NAME	someApp
	remoteLibraryShouldBeImported	someApp
	runKeywordAndIgnoreError	someApp.stopJvm		#verify it has really stopped!
	reimport.startApplication
	reimport.importRemoteLibrary	${remoteLib}	WITH NAME	someApp
	remoteLibraryShouldBeImported	someApp
	[teardown]	runKeywordAndIgnoreError	someApp.stopJvm

Passes Arguments
	passingArguments.startApplication	-Dfoo=bar	baz
	passingArguments.importRemoteLibrary	JavaTools
	passingArguments.importRemoteLibrary	${remoteLib}	WITH NAME	myApp2
	${property}=	getSystemProperty	foo
	@{args}=	myApp2.getArguments
	shouldBeEqualAsStrings	bar	${property}
	shouldBeEqualAsStrings	baz	@{args}
	[teardown]	runKeywordAndIgnoreError	myApp2.stopJvm

Close Application

*Keyword*	*Action*	*Argument*	*Argument*
remoteLibraryShouldBeImported	[arguments]	${library}
	applicationShouldBeRunning	${library}
	runKeyword	${library}.stopApplication
	applicationShouldNotBeRunning	${library}

applicationShouldBeRunning	[arguments]	${library}=myApp
	${appIsRunning}=	runKeyword	${library}.applicationIsRunning
	shouldBeTrue	${appIsRunning}

applicationShouldNotBeRunning	[arguments]	${library}=myApp
	${appIsRunning}=	runKeyword	${library}.applicationIsRunning
	shouldNotBeTrue	${appIsRunning}

shouldFailInApproximatelyOneSecond	[arguments]	${expectedErrorMsg}	${keyword}	@{arguments}
	shouldFailInApproximatelyTime	1	${expectedErrorMsg}	${keyword}	@{arguments}

shouldFailInApproximatelyTenSeconds	[arguments]	${expectedErrorMsg}	${keyword}	@{arguments}
	shouldFailInApproximatelyTime	10	${expectedErrorMsg}	${keyword}	@{arguments}

shouldFailInApproximatelyTime	[arguments]	${timeout}	${expectedErrorMsg}	${keyword}	@{arguments}
	${startTime}=	getTime	epoch
	runKeywordAndExpectError	${expectedErrorMsg}	${keyword}	@{arguments}
	${endTime}=	getTime	epoch
	${runTime}=	evaluate	${endTime} - ${startTime}
	${stoppedBeforeTimeout}=	evaluate	${runTime} < ${timeout}
	${stoppedWithinTreshold}=	evaluate	${runTime} > ${timeout} - 2
	shouldNotBeTrue	${stoppedBeforeTimeout}
	shouldBeTrue	${stoppedWithinTreshold}
