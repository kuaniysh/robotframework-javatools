*** Setting ***

library	ApplicationLauncher	${app}	WITH NAME	myAppLauncher
library	ApplicationLauncher	${app}	1sec	WITH NAME	oneSec
library	ApplicationLauncher	${app}	20sec	WITH NAME	tenSec
library	ApplicationLauncher	${app}	WITH NAME	reimport
library	ApplicationLauncher	${app}	20sec	WITH NAME	passingArguments
library	ApplicationLauncher	${app}	WITH NAME	multipleLauncher
library	ApplicationLauncher	${app}	WITH NAME	closeable
library	ApplicationLauncher	${app}	WITH NAME	logging
resource	applauncher_resource.tsv
#suiteSetup	startAppAndImportSomething

*** Variable ***

${app}	org.robotframework.jvmconnector.mocks.MyApplication
${remoteLib}	org.robotframework.jvmconnector.keyword.JvmconnectorKeywords
${timeoutMsg}	Could not connect to application ${app}

*** Test Case ***

Closes Application That Has Been Started In Suite Setup
	startAppAndImportSomething
	myAppLauncher.closeApplication

Starts Application And Imports Remote Library
	myAppLauncher.startApplication
	myAppLauncher.importRemoteLibrary	${remoteLib}	WITH NAME	myApp
	remoteLibraryShouldBeImported	myApp
	[teardown]	myAppLauncher.closeApplication

Importing Remote Library Times Outs If Application Is Not Started	[tags]	problem
	shouldFailInApproximatelyOneSecond	${timeoutMsg}	oneSec.importRemoteLibrary	${remoteLib}	WITH NAME	thisShouldFail

Importing Remote Library Times Outs If Application Is Stopped	[tags]	problem
	tenSec.startApplication
	tenSec.importRemoteLibrary	${remoteLib}	WITH NAME	stoppedApp
	runKeywordAndIgnoreError	stoppedApp.stopJvm
	shouldFailInApproximatelyTwentySeconds	${timeoutMsg}	tenSec.importRemoteLibrary	${remoteLib}	WITH NAME	thisShouldFail

Reimports After Restart
	reimport.startApplication
	reimport.importRemoteLibrary	${remoteLib}	WITH NAME	someApp
	remoteLibraryShouldBeImported	someApp
	runKeywordAndIgnoreError	someApp.stopJvm
	reimport.startApplication
	reimport.importRemoteLibrary	${remoteLib}	WITH NAME	someApp
	remoteLibraryShouldBeImported	someApp
	[teardown]	reimport.closeApplication

Passes Arguments
	passingArguments.startApplication	baz	-Dfoo=bar
	passingArguments.importRemoteLibrary	JavaTools
	passingArguments.importRemoteLibrary	${remoteLib}	WITH NAME	myApp2
	${property}=	getSystemProperty	foo
	@{args}=	myApp2.getArguments
	shouldBeEqualAsStrings	bar	${property}
	shouldBeEqualAsStrings	baz	@{args}
	[teardown]	passingArguments.closeApplication

Close Application
	closeable.startApplication
	closeable.importRemoteLibrary	${remoteLib}	WITH NAME	closeableApp
	remoteLibraryShouldBeImported	closeableApp
	closeable.closeApplication
	runKeywordAndExpectError	*	closeableApp.applicationIsRunning

*** Keyword ***	

startAppAndImportSomething
	myAppLauncher.startApplication
	myAppLauncher.importRemoteLibrary	SwingLibrary	WITH NAME	remoteLibraryInSuiteSetup

remoteLibraryShouldBeImported	[arguments]	${library}
	applicationShouldBeRunning	${library}
	runKeyword	${library}.stopApplication
	applicationShouldNotBeRunning	${library}

applicationShouldBeRunning	[arguments]	${library}
	${appIsRunning}=	runKeyword	${library}.applicationIsRunning
	shouldBeTrue	${appIsRunning}

applicationShouldNotBeRunning	[arguments]	${library}
	${appIsRunning}=	runKeyword	${library}.applicationIsRunning
	shouldNotBeTrue	${appIsRunning}

shouldFailInApproximatelyOneSecond	[arguments]	${expectedErrorMsg}	${keyword}	@{arguments}
	shouldFailInApproximatelyTime	1	${expectedErrorMsg}	${keyword}	@{arguments}

shouldFailInApproximatelyTwentySeconds	[arguments]	${expectedErrorMsg}	${keyword}	@{arguments}
	shouldFailInApproximatelyTime	20	${expectedErrorMsg}	${keyword}	@{arguments}

shouldFailInApproximatelyTime	[arguments]	${timeout}	${expectedErrorMsg}	${keyword}	@{arguments}
	${startTime}=	getTime	epoch
	runKeywordAndExpectError	${expectedErrorMsg}	${keyword}	@{arguments}
	${endTime}=	getTime	epoch
	${runTime}=	evaluate	${endTime} - ${startTime}
	${stoppedBeforeTimeout}=	evaluate	${runTime} < ${timeout}
	${stoppedWithinTreshold}=	evaluate	${runTime} > ${timeout} - 2
	shouldNotBeTrue	${stoppedBeforeTimeout}
	shouldBeTrue	${stoppedWithinTreshold}
