*** Setting ***

resource	applauncher_resource.tsv
library	org.robotframework.jvmconnector.util.FileServer	WITH NAME	FileServer
library	JavaTools
library	JvmconnectorKeywords
library	JnlpEnhancer	${TEMPDIR}/launcher.txt	${testAppDir}/test-lib
suiteSetup	startWebstartService
suiteTeardown	stopWebstartService

*** Variable ***

${testAppDir}	${CURDIR}/../src/test/resources
${codeBase}	http://localhost:14563/test-app
${jnlpWithMainClassUrl}	${codeBase}/test-application.jnlp
${jnlpWithoutMainClassUrl}	${codeBase}/test-application-withoutmainclass.jnlp
${jnlpWithMainAttribute}	${codeBase}/test-application-with-main-attribute.jnlp
${jnlpWithSsl}	https://localhost:14564/test-app/test-application.jnlp

*** Test Case ***

ApplicationLauncher Launches Java Webstart Application
	applicationLauncherLaunchesJavaWebstartApplication	${jnlpWithMainClassUrl}	Test App

ApplicationLauncher Launches Java Webstart Application When No Main Class Attribute In Jnlp
	applicationLauncherLaunchesJavaWebstartApplication	${jnlpWithoutMainClassUrl}	Other App

ApplicationLauncher Works With Main Attribute
	applicationLauncherLaunchesJavaWebstartApplication	${jnlpWithMainAttribute}	Other App

ApplicationLauncher Works With Https
	setTrustStore
	applicationLauncherLaunchesJavaWebstartApplication	${jnlpWithSsl}	Test App

Can Be Launched Externally
	${pathToJnlp}=	JnlpEnhancer.createRmiEnhancedJnlp	${jnlpWithMainClassUrl}
	start_process	javaws ${pathToJnlp}
	importLibrary	ApplicationLauncher	${pathToJnlp}	WITH NAME	externalLauncher
	setLibrarySearchOrder	externalLauncher
	applicationStarted
	importRemoteLibraries	external
	selectApplication	external
	selectMainWindow
	closeApplication

*** Keyword ***

startWebstartService
	FileServer.start	${testAppDir}

stopWebstartService
	FileServer.stop

applicationLauncherLaunchesJavaWebstartApplication	[arguments]	${jnlpUrl}	${windowTitle}
	importApplicationLauncher	${jnlpUrl}	${windowTitle}
	startApplicationAndImportLibraries	${windowTitle}	${EMPTY}
	selectApplication	${windowTitle}
	waitUntilKeywordSucceeds	30	2	selectWindow	${windowTitle}
	runKeyword	closeApplication

importApplicationLauncher	[arguments]	${jnlpUrl}	${name}
	importLibrary	ApplicationLauncher	${jnlpUrl}		${testAppDir}/test-lib	WITH NAME	${name}Launcher
	selectApplication	${name}

setTrustStore
	JavaTools.setSystemProperty	javax.net.ssl.trustStore	${testAppDir}/jssecacerts
	JavaTools.setSystemProperty	javax.net.ssl.trustStorePassword	changeit
