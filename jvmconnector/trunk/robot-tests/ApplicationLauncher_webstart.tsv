*** Setting ***

resource	applauncher_resource.tsv
library	org.robotframework.jvmconnector.util.FileServer	WITH NAME	FileServer
library	JavaTools
library	JvmconnectorKeywords
suiteSetup	startWebstartService
suiteTeardown	stopWebstartService

*** Variable ***

${testAppDir}	${CURDIR}${/}..${/}src${/}test${/}resources
${codeBase}	http://localhost:14563/test-app
${jnlpWithMainClassUrl}	${codeBase}/test-application.jnlp
${jnlpWithoutMainClassUrl}	${codeBase}/test-application-withoutmainclass.jnlp
${jnlpWithMainAttribute}	${codeBase}/test-application-with-main-attribute.jnlp
${jnlpWithSsl}	https://localhost:14564/test-app/test-application.jnlp

*** Test Case ***

ApplicationLauncher Launches Java Webstart Application
	applicationLauncherLaunchesJavaWebstartApplication	${jnlpWithMainClassUrl}	Test App

ApplicationLauncher Launches Java Webstart Application When No Main Class Attribute In Jnlp
	applicationLauncherLaunchesJavaWebstartApplication	${jnlpWithoutMainClassUrl}	Other App

ApplicationLauncher Works With Main Attribute
	applicationLauncherLaunchesJavaWebstartApplication	${jnlpWithMainAttribute}	Other App

ApplicationLauncher Works With Https
	setTrustStore
	applicationLauncherLaunchesJavaWebstartApplication	${jnlpWithSsl}	Test App

Create RMI Enhanced JNLP
	${pathToJnlp}=	createRmiEnhancedJnlp	${jnlpWithMainClassUrl}
	${expectedJnlpContents}=	getEnhancedJnlp	${testAppDir}${/}test-lib	${jnlpWithMainClassUrl}
	${jnlpContents}=	getFile	${pathToJnlp}
	shouldBeEqual	${expectedJnlpContents}	${jnlpContents}

*** Keyword ***

startWebstartService
	FileServer.start	${testAppDir}

stopWebstartService
	FileServer.stop

applicationLauncherLaunchesJavaWebstartApplication	[arguments]	${jnlpUrl}	${windowTitle}
	importLibrary	ApplicationLauncher	${jnlpUrl}	WITH NAME	${windowTitle}Launcher
	startApplicationAndImportLibraries	${windowTitle}	${EMPTY}
	selectApplication	${windowTitle}
	waitUntilKeywordSucceeds	30	2	${windowTitle}.selectWindow	${windowTitle}
	runKeyword	closeApplication

setTrustStore
	setSystemProperty	javax.net.ssl.trustStore	${testAppDir}${/}jssecacerts
	setSystemProperty	javax.net.ssl.trustStorePassword	changeit
